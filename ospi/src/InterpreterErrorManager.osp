use aves;
use osprey.compiler;

namespace osprey.interpreter;

internal class InterpreterErrorManager is ErrorManager
{
	public new(this._app);

	private _app;
	/// Summary: Gets the {App} that this error manager belongs to.
	public get app => _app;

	private errors = [];
	private warnings = [];

	override get hasErrors => not errors.isEmpty;

	override get hasWarnings => not warnings.isEmpty;

	override addErrorInner(error)
	{
		errors.add(error);
		app.printError(error);
	}

	override getAllErrors()
	{
		return errors.slice(0);
	}

	override clearErrors()
	{
		errors = [];
	}

	override addWarningInner(warning)
	{
		warnings.add(warning);
		app.printWarning(warning);
	}

	override getAllWarnings()
	{
		return warnings.slice(0);
	}

	override clearWarnings()
	{
		warnings = [];
	}
}
